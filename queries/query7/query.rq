# Query 7: Retrieve in-depth information about a specific product including offers and reviews

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rev: <http://purl.org/stuff/rev#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
PREFIX bsbm-inst-data: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/dataFromProducer1/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

SELECT ?offer ?price ?vendor ?vendorTitle ?review ?revTitle ?reviewer ?revName ?rating1 ?rating2
WHERE {
	bsbm-inst-data:Product10 rdfs:label ?productLabel .
	OPTIONAL {
		?offer bsbm:product bsbm-inst-data:Product10 .
		?offer bsbm:price ?price .
		?offer bsbm:vendor ?vendor .
		?vendor rdfs:label ?vendorTitle .
		?vendor bsbm:country <http://downlode.org/rdf/iso-3166/countries#GB> .
		?offer dc:publisher ?vendor .
		?offer bsbm:validTo ?date .
		FILTER ( ?date >= "2008-07-19T00:00:00"^^<http://www.w3.org/2001/XMLSchema#dateTime> )
	}
	OPTIONAL {
		?review bsbm:reviewFor bsbm-inst-data:Product10 .
		?review rev:reviewer ?reviewer .
		?reviewer foaf:name ?revName .
		?review dc:title ?revTitle .
		OPTIONAL { ?review bsbm:rating1 ?rating1 . }
		OPTIONAL { ?review bsbm:rating2 ?rating2 . }
	}
}
