# Query 2: Retrieve basic information about a specific product for display purposes

PREFIX bsbm-inst-data: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/dataFromProducer1/>
PREFIX bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

SELECT ?label ?comment ?producer ?productFeature ?propertyTextual1 ?propertyTextual2 ?propertyTextual3 ?propertyNumeric1 ?propertyNumeric2 ?propertyTextual4 ?propertyTextual5 ?propertyNumeric4
WHERE {
	bsbm-inst-data:Product14 rdfs:label ?label .
	bsbm-inst-data:Product14 rdfs:comment ?comment .
	bsbm-inst-data:Product14 bsbm:producer ?p .
	?p rdfs:label ?producer .
	bsbm-inst-data:Product14 dc:publisher ?p .
	bsbm-inst-data:Product14 bsbm:productFeature ?f .
	?f rdfs:label ?productFeature .
	bsbm-inst-data:Product14 bsbm:productPropertyTextual1 ?propertyTextual1 .
	bsbm-inst-data:Product14 bsbm:productPropertyTextual2 ?propertyTextual2 .
	bsbm-inst-data:Product14 bsbm:productPropertyTextual3 ?propertyTextual3 .
	bsbm-inst-data:Product14 bsbm:productPropertyNumeric1 ?propertyNumeric1 .
	bsbm-inst-data:Product14 bsbm:productPropertyNumeric2 ?propertyNumeric2 .
	OPTIONAL { bsbm-inst-data:Product14 bsbm:productPropertyTextual4 ?propertyTextual4 }
	OPTIONAL { bsbm-inst-data:Product14 bsbm:productPropertyTextual5 ?propertyTextual5 }
	OPTIONAL { bsbm-inst-data:Product14 bsbm:productPropertyNumeric4 ?propertyNumeric4 }
}
